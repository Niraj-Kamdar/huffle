/* Functions */
#define function enterRaffle() payable returns ()
#define function getEntranceFee() view returns (uint256)
#define function getPlayer(uint256) view returns (address)
#define function getInterval() view returns (uint256)

/* Errors */
#define error Raffle_NotEnoughEthSent()

/* Events */
#define event RaffleEnter(address indexed player)

/* Storage Slots */
#define constant ENTRANCE_FEE_SLOT = FREE_STORAGE_POINTER()
#define constant PLAYERS_SLOT = FREE_STORAGE_POINTER()
#define constant INTERVAL_SLOT = FREE_STORAGE_POINTER()

// Get length
#define macro PLAYERS_ARRAY_LENGTH() = takes(0) returns(1) {
    [PLAYERS_SLOT] sload   // [length]
}

// Get element at index
#define macro PLAYERS_ARRAY_GET() = takes(1) returns(1) {
    0x20 mul             // [array_offset] index * 32 bytes
    [PLAYERS_SLOT]       // [array_slot, array_offset]
    0x00 mstore          // [array_offset] (store slot ptr in memory)
    0x20 0x00 sha3       // [data_start, array_offset] (keccak256 of slot)
    add                  // [element_slot]
    sload                // [value]
}

// Push element
#define macro PLAYERS_ARRAY_PUSH() = takes(1) returns(0) {
    [PLAYERS_SLOT] sload    // [length, caller]
    dup1                    // [length, length, caller]
    0x01 add                // [new_length, length, caller]
    [PLAYERS_SLOT] sstore   // [length, caller] (update length)
    
    [PLAYERS_SLOT]          // [array_slot, length, caller]
    0x00 mstore            // [length, caller]
    0x20 0x00 sha3         // [data_start, length, caller] keccak(player_slot) -> data_slot
    swap1                  // [length, data_start, caller]
    0x20 mul               // [offset, data_start, caller] offset = length * 32
    add                    // [new_element_slot, caller]
    sstore                 // []
}

#define macro GET_ENTRANCE_FEE() = takes (0) returns (0) {
    // Load value from storage.
    [ENTRANCE_FEE_SLOT]   // [ptr]
    sload                // [value]

    // Store value in memory.
    0x00 mstore

    // Return value
    // len ptr
    0x20 0x00 return
}

#define macro GET_INTERVAL() = takes (0) returns (0) {
    // Load value from storage.
    [INTERVAL_SLOT]      // [ptr]
    sload                // [value]

    // Store value in memory.
    0x00 mstore

    // Return value
    // len ptr
    0x20 0x00 return
}

#define macro ENTER_RAFFLE() = takes (0) returns (0) {
    // Check if msg.value >= entrance fee
    callvalue                           // Stack: [msg.value]
    [ENTRANCE_FEE_SLOT] sload          // Stack: [entranceFee, msg.value]
    lt                                  // Stack: [msg.value < entranceFee]
    error jumpi                         // Jump to error if true
    
    caller
    PLAYERS_ARRAY_PUSH()

    caller
    __EVENT_HASH(RaffleEnter)
    0x00 0x00
    log2
    
    stop
    
    error:
        __ERROR(Raffle_NotEnoughEthSent)  // Load error selector
        0x00 mstore                       // Store at memory position 0
        0x04 0x00 revert                  // Revert with 4 bytes of error data
}


#define macro CONSTRUCTOR() = takes (0) returns (0) {
    // Copy the first argument (entrance_fee) into memory
    0x20        // [size]
    0x40        // [totalSize, size]
    codesize    // [codesize, totalSize, size]
    sub         // [offset, size]
    0x00        // [mem, offset, size]
    codecopy    // [] -> copies data to memory

    // Store the first argument (entrance_fee) in storage
    0x00 mload  // [entrance_fee]
    [ENTRANCE_FEE_SLOT] // [location, entrance_fee]
    sstore

    // Copy the second argument (interval) into memory
    0x20        // [size]
    0x20        // [remainingSize, size]
    codesize    // [codesize, remainingSize, size]
    sub         // [offset, size]
    0x00        // [mem, offset, size]
    codecopy    // [] -> copies data to memory

    // Store the second argument (interval) in storage
    0x00 mload      // [interval]
    [INTERVAL_SLOT] // [location, interval]
    sstore
}

#define macro MAIN() = takes (0) returns (0) {
    // Identify which function is being called.
    0x00 calldataload 0xE0 shr // stack: [selector]
    dup1 __FUNC_SIG(getEntranceFee) eq getEntranceFee jumpi // stack [selector]
    dup1 __FUNC_SIG(enterRaffle) eq enterRaffle jumpi 
    dup1 __FUNC_SIG(getPlayer) eq getPlayer jumpi
    __FUNC_SIG(getInterval) eq getInterval jumpi

    0x00 0x00 revert

    getEntranceFee:
        GET_ENTRANCE_FEE()
    enterRaffle:
        ENTER_RAFFLE()
    getPlayer:
        0x04 calldataload // stack: [index]
        PLAYERS_ARRAY_GET() // stack: [element]
        0x00 mstore          // []
        0x20 0x00 return
    getInterval:
        GET_INTERVAL()
}